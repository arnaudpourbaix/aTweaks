
INCLUDE FILE(include/definitions.ssl)

//Priest or not?
IF
  Global("flPriestSetup","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("flPriest","LOCALS",1)
    SetGlobal("flPriestSetup","LOCALS",1)
  RESPONSE #100
    SetGlobal("flPriestSetup","LOCALS",1)
END

//Aura
IF
  !StateCheck(Myself,STATE_REALLY_DEAD)
  !GlobalTimerNotExpired("flAura","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("flAura","LOCALS",18)
    ReallyForceSpellRES("fl#zomse",Myself)
    Continue()
END

INCLUDE FILE(include/baseinitial.ssl)

//Spells
BEGIN LOOP(loopvar||2;1;6;5;4;3)
  IF TRIGGER
    Target(Playerloopvar)
    Global("flPriest","LOCALS",1)
    TriggerBlock(Enemy|Disabled|SpellTurning|SIAlt|SIEnc|MinorGlobe)
    !GlobalTimerNotExpired("flDoomedPlayerloopvar","LOCALS")
  THEN DO
    SetGlobalTimer("flDoomedPlayerloopvar","LOCALS",60)
    Action(Spell,CLERIC_DOOM|100|25)
  END
  
  IF TRIGGER
    Target(Playerloopvar)
    Global("flPriest","LOCALS",1)
    TriggerBlock(Enemy|Disabled|SpellTurning|SIEnc|MinorGlobe)
    GlobalTimerNotExpired("flDoomedPlayerloopvar","LOCALS")
  THEN DO
      Action(Spell,CLERIC_HOLD_PERSON)
  END
END LOOP

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  Global("flPriest","LOCALS",1)
  TriggerBlock(Enemy|Disabled|SpellTurning|SIEnc|MinorGlobe)
THEN DO
    Action(Spell,CLERIC_COMMAND|100|25)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  Global("flPriest","LOCALS",1)
  TriggerBlock(Enemy|Disabled|SpellTurning|SIEnc|MinorGlobe)
THEN DO
    Action(Spell,CLERIC_HOLD_PERSON|100|100)
END

BEGIN LOOP(DamageRes||CrushRes)
  INCLUDE FILE(general/melee_mid.ssl)
END LOOP

INCLUDE FILE(include/open_door.ssl)

INCLUDE FILE(include/baseend.ssl)
