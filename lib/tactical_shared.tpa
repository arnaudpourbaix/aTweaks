INCLUDE ~%MOD_FOLDER%/lib/symbols.tpa~
INCLUDE ~%MOD_FOLDER%/lib/ds/item_labels.tpa~

ACTION_IF NOT (FILE_EXISTS_IN_GAME fl#labelled_weapons.mrk OR MOD_IS_INSTALLED "setup-stratagems.tp2" "5900") BEGIN
  INCLUDE ~%MOD_FOLDER%/lib/DS/label_weapons.tph~                                        //Lifted from SCSII
  OUTER_SET ~weapon_enchantment_stat~=109
  OUTER_SET ~weapon_enchantment_type~=233
  // The assumption is that slot 109 ("CLERIC_HALLOW") is used to indicate weapon enchantment. It's a proficiency, not a scripting state - that's what the ~type~ codes
  LAM ~label_weapons~
  COPY_EXISTING sw1h01.itm "override/fl#labelled_weapons.mrk"
END ELSE COPY_EXISTING sw1h01.itm "override/fl#labelled_weapons.mrk"

ACTION_IF NOT FILE_EXISTS_IN_GAME fl#labelled_item.mrk BEGIN
  LAF item_labels END
  COPY_EXISTING sw1h01.itm "override/fl#labelled_item.mrk"
END

ACTION_IF !FILE_EXISTS_IN_GAME rr#trat.cre BEGIN                                    // TODO: Purpose of this file ???
  COPY ~%MOD_FOLDER%/cre/rr#trat.cre~ ~override~                                         // Targeting Rat (script aid)
  COMPILE ~%MOD_FOLDER%/baf/rr#trat.baf~                                                 // Targeting Rat AI script
END

ACTION_IF !FILE_EXISTS_IN_GAME rr#vsbl.spl BEGIN
  COPY ~%MOD_FOLDER%/spl/rr#vsbl.spl~ ~override~                                         // Spell which turns the target visible but doesn't remove Improved Invisibility
END

ACTION_IF !FILE_EXISTS_IN_GAME rr#usumn.spl BEGIN
  COPY ~%MOD_FOLDER%/bam/rr#usumn.bam~ ~override~                                        // Unsummon BAM icon
  COPY ~%MOD_FOLDER%/spl/rr#usumn.spl~ ~override~                                        // Spell which allows the player to unsummon a creature
    SAY NAME1 @2132 SAY NAME2 @2132
END

// Add new secondary types; this is already non-cumulative
ADD_SECTYPE RR#ETHER @707
ADD_SECTYPE fl#Disease #2449
ADD_SECTYPE fl#Fear @1027
ADD_SECTYPE fl#Poison #14028

// Add new projectiles; this is also non-cumulative
ADD_PROJECTILE ~%MOD_FOLDER%/PRO/RR#SPAIN.PRO~                                          // New projectile for Symbol Pain (party friendly)
ADD_PROJECTILE ~%MOD_FOLDER%/pro/fl#cnenp.pro~                                          // Invisible, 90° cone-shaped projectile, party-friendly
ADD_PROJECTILE ~%MOD_FOLDER%/pro/fl#shout.pro~                                          // shout, 30° cone-shaped projectile, party-friendly
ADD_PROJECTILE ~%MOD_FOLDER%/pro/fl#sight.pro~                                          // Like INAREANP, but extends roughly as far as you can see
ADD_PROJECTILE ~%MOD_FOLDER%/pro/rr#pyrot.pro~                                          // New projectile for Pyrotechnics
ADD_PROJECTILE ~%MOD_FOLDER%/pro/rr#fcsng.pro~                                          // New projectile for Charming Song
ADD_PROJECTILE ~%MOD_FOLDER%/pro/rr#icew.pro~                                           // New projectile for Wall of Ice
ADD_PROJECTILE ~%MOD_FOLDER%/pro/rr#vrpo.pro~                                           // New projectile for Wind Wall
ADD_PROJECTILE ~%MOD_FOLDER%/pro/rr#cloud.pro~                                          // cloud spell effect with party-friendly

// We forcibly reload all PRO variables, because there's a bug in WeiDU 231
// TODO: check if it is fixed in WeiDU 249+
// COPY_EXISTING missile.ids override
//   READ_2DA_ENTRIES_NOW buff 2
//   PATCH_FOR_EACH pro IN RR#SPAIN fl#cnenp fl#sight rr#pyrot rr#fcsng rr#icew rr#vrpo BEGIN
//     FOR (i = buff - 1; i >= 0; --i) BEGIN
//       READ_2DA_ENTRY_FORMER buff i 1 label
//       PATCH_IF "%label%" STRING_EQUAL_CASE "%pro%" BEGIN
//         READ_2DA_ENTRY_FORMER buff i 0 real_value
//         SET EVAL "%pro%" = real_value
//         i = "-1"
//       END
//     END
//   END
// BUT_ONLY

// Add shared VVCs and BAMs
COPY ~%MOD_FOLDER%/bam/elementals/rr#hcold.bam~ ~override~                              // IWD cold damage hit BAM animation
COPY ~%MOD_FOLDER%/vvc/rr#hcold.vvc~ ~override~                                         // IWD cold damage hit VVC animation
COPY ~%MOD_FOLDER%/wav/rr#hcold.wav~ ~override~                                         // IWD cold damage hit WAV sound

COPY ~%MOD_FOLDER%/bam/elementals/rr#hfire.bam~ ~override~                              // IWD fire damage hit BAM animation
COPY ~%MOD_FOLDER%/vvc/rr#hfire.vvc~ ~override~                                         // IWD fire damage hit VVC animation
COPY ~%MOD_FOLDER%/wav/rr#hfire.wav~ ~override~                                         // IWD fire damage hit WAV sound

COPY ~%MOD_FOLDER%/bam/elementals/rr#helec.bam~ ~override~                              // IWD electricity damage hit BAM animation
COPY ~%MOD_FOLDER%/vvc/rr#helec.vvc~ ~override~                                         // IWD electricity damage hit VVC animation
COPY ~%MOD_FOLDER%/wav/rr#helec.wav~ ~override~                                         // IWD electricity damage hit WAV sound

COPY ~%MOD_FOLDER%/bam/elementals/rr#hacid.bam~ ~override~                              // IWD acid damage hit BAM animation
COPY ~%MOD_FOLDER%/vvc/rr#hacid.vvc~ ~override~                                         // IWD acid damage hit VVC animation
COPY ~%MOD_FOLDER%/wav/rr#hacid.wav~ ~override~                                         // IWD acid damage hit WAV sound

COPY ~%MOD_FOLDER%/vvc/rr#ecems.vvc~ ~override~                                         // Cold Aura VVC animation
COPY ~%MOD_FOLDER%/bam/elementals/rr#ecems.bam~ ~override~                              // Cold Aura BAM animation

COPY ~%MOD_FOLDER%/vvc/rr#ehems.vvc~ ~override~                                         // Fire Aura VVC animation
COPY ~%MOD_FOLDER%/bam/elementals/rr#ehems.bam~ ~override~                              // Fire Aura BAM animation

COPY ~%MOD_FOLDER%/bam/elementals/rr#ceart.bam~ ~override~                              // IWD Conjure Earth Elemetnal BAM animation
COPY ~%MOD_FOLDER%/vvc/rr#ceart.vvc~ ~override~                                         // IWD Conjure Earth Elemetnal VVC animation

COPY ~%MOD_FOLDER%/bam/elementals/rr#cfire.bam~ ~override~                              // IWD Conjure Fire Elemetnal BAM animation
COPY ~%MOD_FOLDER%/vvc/rr#cfire.vvc~ ~override~                                         // IWD Conjure Fire Elemetnal VVC animation

COPY ~%MOD_FOLDER%/bam/elementals/rr#cwatr.bam~ ~override~                              // IWD Conjure Water Elemetnal BAM animation
COPY ~%MOD_FOLDER%/vvc/rr#cwatr.vvc~ ~override~                                         // IWD Conjure Water Elemetnal VVC animation

COPY ~%MOD_FOLDER%/bam/rr#shout.bam~ ~override~                              		       // IWD shoutt BAM animation


// Assign the Enchantment school to enchantment based spell-like abilities and make them turn the caster visible
ACTION_IF !GAME_IS ~iwdee~ BEGIN // not on IWD:EE
  COPY_EXISTING ~SPWI929.SPL~ ~override~     // Succubus Charm Female
                ~SPWI930.SPL~ ~override~     // Succubus Charm Male
    WRITE_ASCII 0x10 CAS_M05 #8              // casting sound: CAS_M05.WAV
    WRITE_SHORT 0x25 4                       // spell school: 4 (Enchantment)
    WRITE_SHORT 0x22 11                      // casting graphics: 11 (Enchantment)
    WRITE_LONG  0x34 1                       // spell level: 1
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 136 target = 1 timing = 1 END   // force visible self
    LPF FJ_SPL_ITM_REINDEX END
  BUT_ONLY_IF_IT_CHANGES
END

// Fix a mistargeted opcode in the Death Ward spell which prevents Detectable Spells from noticing it correctly (mirrors G3 BG2 Fixpack code)
ACTION_IF !GAME_IS ~iwdee~ BEGIN // not on IWD:EE
COPY_EXISTING ~sppr409.spl~ ~override~
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (i=0; i<abil_num; ++i) BEGIN
    READ_SHORT abil_off+0x1e+i*0x28 abil_fx_num
    READ_SHORT abil_off+0x20+i*0x28 abil_fx_idx
    FOR (j=0; j<abil_fx_num; ++j) BEGIN
      READ_SHORT fx_off+0x30*(abil_fx_idx+j) opcode
      PATCH_IF opcode = 282 BEGIN // Scripting State Modifier
        WRITE_BYTE fx_off+0x02+0x30*(abil_fx_idx+j) 2 // preset target
      END
    END
  END
BUT_ONLY_IF_IT_CHANGES
END


// Revised Critical Hit feedback string (removes helmet reference)
STRING_SET ~20696~ @2122   // Critical Hit Averted


// Externalize cause wound item effects to spells
ACTION_IF !GAME_IS ~iwdee~ BEGIN // not on IWD:EE
  ACTION_IF !FILE_EXISTS_IN_GAME ~rr#csw.spl~ BEGIN                                    // safety check, do this only once
    COPY ~%MOD_FOLDER%/spl/rr#csw.spl~ ~override~                                           // Cause Serious Wounds (shell spell)
    COPY ~%MOD_FOLDER%/spl/rr#ccw.spl~ ~override~                                           // Cause Critical Wounds (shell spell)
    COPY ~%MOD_FOLDER%/spl/rr#harm.spl~ ~override~                                          // Harm (shell spell)
    ACTION_DEFINE_ASSOCIATIVE_ARRAY cause_wounds BEGIN
      serious => rr#csw   // Cause Serious Wounds
      critical => rr#ccw  // Cause Critical Wounds
      harm => rr#harm     // Harm
    END
    ACTION_PHP_EACH cause_wounds AS rr#itm => rr#spl BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~%rr#itm%.itm~ BEGIN
        COPY_EXISTING ~%rr#itm%.itm~ ~override~
          PATCH_IF SOURCE_SIZE > 0x71 THEN BEGIN
            LPF ITEM_EFFECT_TO_SPELL INT_VAR type=99 STR_VAR new_itm_spl=EVALUATE_BUFFER ~%rr#spl%.spl~ END
            LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete="-1" END   // delete all effects from the item's extended header
            LPF ADD_ITEM_EFFECT INT_VAR opcode=146   // Cast Spell at Target
                                        target=2     // Preset Target
                                        timing=1     // Permanent
                                        parameter2=1 // Cast Instantly
                                        type=99
                                STR_VAR resource=EVALUATE_BUFFER "%rr#spl%"
            END
          END
        BUT_ONLY_IF_IT_CHANGES
      END
    END
  END
END

// Tag celestials, for detectability (mirrors code in SCSII)
ACTION_FOR_EACH celestial IN
  devagood
  devaevil
  plangood
  planevil
  ca#dastr
  ca#dmond
  ca#dmova
  ca#plan
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%celestial%.cre" BEGIN
    COPY_EXISTING "%celestial%.cre" override
      WRITE_BYTE 0x274 182 //Specific
    BUT_ONLY
  END
END

// Ascension removes ipsion.itm from Magical Swords
ACTION_IF FILE_EXISTS_IN_GAME sword01.cre AND NOT FILE_CONTAINS_EVALUATED(sword01.cre "ipsion") BEGIN
  COPY_EXISTING sword01.cre override
    ADD_CRE_ITEM ipsion #0 #0 #0 none amulet
  BUT_ONLY
END
