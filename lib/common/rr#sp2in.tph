// Convert spell into innate ability macro
// Useful for assigning spells-like abilities to creatures who have no caster levels

DEFINE_PATCH_FUNCTION ~RR#SP2IN~  

INT_VAR
  ctime  = 1 // new casting time, set to 99 to leave as is
  rinvs  = 0 // spell removes invisibility when cast
  renew  = 0 // spell renews after being cast
BEGIN
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  WRITE_SHORT 0x1c 4 // Set spell type to Innate (4)
  WRITE_LONG  0x34 1 // Set spell level to 1 to avoid scripting issues

  FOR (i=0; i<~abil_num~; i=i+1) BEGIN
    WRITE_BYTE ~abil_off~+0x02+0x28*~i~ 4                        // change ability icon location to Innate (4)
    PATCH_IF ctime != 99 BEGIN
      WRITE_SHORT ~abil_off~+0x12+0x28*~i~ ctime              // set casting time to the predetermined value
    END
  END

  PATCH_IF rinvs = 1 BEGIN // if the spell should remove invisibility
    LPF ADD_SPELL_EFFECT INT_VAR opcode=136 target=1 timing=1 END
  END
  PATCH_IF renew = 1 BEGIN // if the spell should be renewed after being cast
    LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=1 STR_VAR resource=EVAL %DEST_RES% END
    LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=1 STR_VAR resource=EVAL %DEST_RES% END
  END
  LPF FJ_SPL_ITM_REINDEX END
END
