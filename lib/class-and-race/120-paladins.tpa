
COPY_EXISTING ~SPPR317.SPL~ ~override/RR#PALCD.SPL~                                // Clone Cure Disease into an innate ability
  READ_BYTE  0x19 "flags"
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  WRITE_SHORT 0x1c 0x04                                                            // Set ability type to Innate (4)
  WRITE_BYTE 0x19 ("%flags%" BOR 0b01000000)                                       // add the 'non-magical' flag
  WRITE_LONG 0x34 1                                                                // Set spell level to 1 to avoid scripting issues
  FOR ("index" = 0; "%index%" < "%abil_num%"; "index" = ("%index%" + 1)) BEGIN     // cycle through headers
    WRITE_BYTE  ("%abil_off%" + 0x02 + (0x28 * "%index%")) 4                       // change ability icon location to Innate (4)
    WRITE_SHORT ("%abil_off%" + 0x12 + ("%index%" * 0x28)) 0                       // set casting time to 0 (in PnP, Paladins can cure diseases by simply touching the recipient)
  END
BUT_ONLY_IF_IT_CHANGES

COPY ~aTweaks/SPL/RR#PALDI.SPL~ ~override~                                         // spell which grants disease immunity


// Add innate disease immunity to various Paladin kits

ACTION_FOR_EACH ~file~ IN
              ~clabpa01~                                                           // True Paladin
              ~clabpa02~                                                           // Cavalier
              ~clabpa03~                                                           // Inquisitor
              ~clabpa04~                                                           // Undead Hunter
                ~KishHL~                                                           // Holy Liberator (from Oversight)
              ~tcselune~                                                           // Paladin of Selune (from Classic Adventures)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ BEGIN
    APPEND ~%file%.2da~ ~ABILITYV AP_RR#PALDI~ UNLESS ~ABILITYV~                   // grant innate disease immunity
  END
END


// Add Cure Disease ability to various Paladin kits

ACTION_FOR_EACH ~file~ IN
              ~clabpa01~                                                           // True Paladin
              ~clabpa02~                                                           // Cavalier
                ~KishHL~                                                           // Holy Liberator (from Oversight)
              ~tcselune~                                                           // Paladin of Selune (from Classic Adventures)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ BEGIN
    APPEND ~%file%.2da~ ~ABILITYW GA_RR#PALCD~ UNLESS ~ABILITYW~                   // grant Cure Disease ability
  END
END


ACTION_FOR_EACH ~file~ IN
              ~clabpa01~                                                           // True Paladin
              ~clabpa02~                                                           // Cavalier
              ~clabpa03~                                                           // Inquisitor
              ~clabpa04~                                                           // Undead Hunter
                ~KishHL~                                                           // Holy Liberator (from Oversight)
              ~tcselune~                                                           // Paladin of Selune (from Classic Adventures)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ BEGIN
    COPY_EXISTING ~%file%.2da~ ~override~
      PATCH_IF (%SOURCE_SIZE% > 0) THEN BEGIN                                      // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
        REPLACE_TEXTUALLY ~AP_RR#PALDI~ ~AP_RR#PALDI **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~ // Add empty designations just to be safe
        REPLACE_TEXTUALLY ~GA_RR#PALCD~ ~GA_RR#PALCD **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~ // Add empty designations just to be safe
        PRETTY_PRINT_2DA
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END
