
COPY ~aTweaks/EFF/RR#IDIM1.EFF~ ~override~                                 // effect file which grants immunity to the inteligence drain opcode
     ~aTweaks/EFF/RR#IDIM2.EFF~ ~override~                                 // effect file which prevents the inteligence drained icon from showing

COPY ~atweaks/eff/rr#idim1.eff~ ~override/fl#idim3.eff~                    //Immunity to display string: int drain
  WRITE_LONG 0x10 267
  WRITE_LONG 0x1c 32089 //devour brain

ACTION_FOR_EACH ~file~ IN
                ~RING95~                                                   // ring which provides undead traits for most undead creatures in the game
                ~RING99~                                                   // ring which provides undead traits for summoned skeletons
                ~_RING95~                                                  // ring which provides undead traits for most undead creatures in the game (Tutu)
                ~_RING99~                                                  // ring which provides undead traits for summoned skeletons (Tutu)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.itm~ BEGIN
    COPY_EXISTING ~%file%.itm~ ~override~
      PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                           // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
        PATCH_FOR_EACH eff IN rr#idim1 rr#idim2 fl#idim3 BEGIN
          PATCH_FOR_EACH class IN 134 135 170 171 172 BEGIN
            LPF ADD_ITEM_EQEFFECT
              INT_VAR
                opcode = 177       //use eff
                target = 1
                timing = 2
                parameter1 = class
                parameter2 = 5     //class.ids
              STR_VAR
                resource = EVAL "%eff%"
            END
          END
          PATCH_FOR_EACH race IN 115 132 133 134 BEGIN
            LPF ADD_ITEM_EQEFFECT
              INT_VAR
                opcode = 177
                target = 1
                timing = 2
                parameter1 = race
                parameter2 = 4     //race.ids
              STR_VAR
                resource = EVAL "%eff%"
            END
          END
        END
      END
    BUT_ONLY
  END
END
