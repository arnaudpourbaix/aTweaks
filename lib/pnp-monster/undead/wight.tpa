// Wight
ACTION_FOR_EACH file IN
                Knightsk //BG1
                _nightsk //BG1
                nevm1 //Nev's
				MH#WIGHT //Made in heaven Q&E
				MH#WGHT5 //Made in heaven Q&E
				MH#WGHT6 //Made in heaven Q&E
				MH#WGHT7 //Made in heaven Q&E
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      LPF FJ_CRE_VALIDITY END
      PATCH_INCLUDE "%MOD_FOLDER%/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "%MOD_FOLDER%/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM "%ring95%" #0 #0 #0 none lring //Standard undead immunities
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none rring
		PATCH_IF !FILE_CONTAINS_EVALUATED (~%SOURCE_FILE%~ ~SPER03~) BEGIN                  // if the creature does possess sper03.itm
          REPLACE_CRE_ITEM fl#wigh #0 #0 #0 none weapon1 EQUIP
		END
    PATCH_IF NOT MOD_IS_INSTALLED ~INFINITYANIMATIONS.TP2~ ~400~ BEGIN // Infinity Animation Compatibility (IAv5b)
        WRITE_LONG 0x28 30464 //animation: ghoul
	END
        LPF patchCreature
          INT_VAR
            xpv = 1400
            hp = 35 //4d8+3
            ac = 5
            thac0 = 15
            apr = 1
            saveDeath = 12
            saveWand = 14
            savePolymorph = 13
            saveBreath = 15
            saveSpell = 13
            level1 = 5
            level2 = 0
            level3 = 0
            resistMagic = 0
            resistFire = 0
            resistCold = 100
            resistElectricity = 0
            resistAcid = 0
            resistSlashing = 0
            resistCrushing = 0
            resistPiercing = 0
            resistMissile = 0
            strength = 16
            strengthx = 0
            intelligence = 10
            wisdom = 9
            dexterity = 9
            constitution = 9
            charisma = 9
            morale = 14
            alignment = 0x13 //LE
            general = 4 //undead
            
            enforce = 1
	       STR_VAR
            notenf01 = MH#WIGHT5
			notenf02 = MH#WIGHT6
			notenf03 = MH#WIGHT7
			notenf04 = Knightsk
			notenf05 = _nightsk
        END
        LPF patchCreatureScript
          STR_VAR
            newscript = fl#midmc
            script01 = wdasight
            script02 = bpwtsigt
			script03 = DW2MC2GW
			script04 = DW2MC1GE
        END
        // LPF cold_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY_EXISTING b1-4.itm "override/fl#wigh.itm"
  LPF set_enchantment END
  LPF cold_spell_immunities END
  DEFINE_ARRAY wigh_opcode BEGIN 216 139 141 END //drain 1 level
  DEFINE_ARRAY wigh_p1 BEGIN 1 41495 0 END
  DEFINE_ARRAY wigh_p2 BEGIN 0 0 1 END
  PHP_EACH wigh_opcode AS int => opcode BEGIN
    LPF ADD_ITEM_EFFECT
      INT_VAR
        type = 1

        opcode
        target = 2
        parameter1 = $wigh_p1("%int%")
        parameter2 = $wigh_p2("%int%")
        timing = 1
     END
   END
