ACTION_IF NOT FILE_EXISTS_IN_GAME ~RR#DDOOR.BAM~ THEN BEGIN       // Checks for the Icewind Dale's Dimension Door animation component
  COPY    ~aTweaks/BAM/SPDIMNDR.BAM~   ~override/RR#DDOOR.BAM~    // IWD1's Dimension Door animation (custom file name)
  COPY    ~aTweaks/WAV/EFF_M09.WAV~    ~override/RR#DDOOR.WAV~    // IWD1's Dimension Door sound effect (custom file name)
  COPY    ~aTweaks/VVC/RR#DDOOR.VVC~   ~override~                 // Custom Dimension Door VVC
END

COPY ~atweaks/spl/rr#telwe.spl~ ~override/rr#ddoor.spl~           // Dimension Door (renewable)
  SAY NAME1 #12082 SAY NAME2 #12082
  LPF spellToInnate INT_VAR ctime=1 renew=1 END

COPY ~atweaks/spl/rr#telwe.spl~ ~override/rr#ddor2.spl~           // Dimension Door (non-renewable, for Nymphs)
  SAY NAME1 #12082 SAY NAME2 #12082
  LPF spellToInnate INT_VAR ctime=1 END

COPY ~atweaks/spl/rr#telwx.spl~ ~override/rr#ddorx.spl~           // Dimension Door (special version, breaks targeting via 1 second invisibility)
  SAY NAME1 #12082 SAY NAME2 #12082
  LPF spellToInnate INT_VAR ctime=1 END

COPY ~atweaks/spl/fey/rr#fafrn.spl~ ~override~                    // Animal Friendship (renewable)
  SAY NAME1 @1900 SAY NAME2 @1900

COPY ~atweaks/spl/fey/rr#fbbea.spl~ ~override~                    // Blinding Beauty (part 1)
  SAY NAME1 @1901 SAY NAME2 @1901
  READ_LONG  0x64 ab_off
  READ_SHORT 0x68 ab_num
  FOR(i=0; i<ab_num; i+=1) BEGIN
    WRITE_SHORT (ab_off+i*0x28+0x26) %fl#sight%                   // use new projectile
  END

COPY ~atweaks/spl/fey/rr#fbbe2.spl~ ~override~                    // Blinding Beauty (part 2)
COPY ~atweaks/eff/fey/rr#fbbea.eff~ ~override~                    // Blinding Beauty EFF (for targeting only humanoids)

COPY ~atweaks/spl/fey/rr#fchrm.spl~ ~override~                    // Charm Person (renewable)
COPY ~atweaks/eff/fey/rr#fchrm.eff~ ~override~                    // Charm Person EFF (for elven and half-elven charm immunity)
  SAY 0x1C @1903                                                  // Resists charm effect

COPY ~atweaks/spl/fey/rr#fcsng.spl~ ~override~                    // Charming Song (part 1)
  SAY NAME1 @1902 SAY NAME2 @1902
  READ_LONG  0x64 ab_off
  READ_SHORT 0x68 ab_num
  FOR(i=0; i<ab_num; i+=1) BEGIN
    WRITE_SHORT (ab_off+i*0x28+0x26) %rr#fcsng%                   // use new projectile
  END

COPY ~atweaks/spl/fey/rr#fcsn2.spl~ ~override~                    // Charming Song (part 2)
COPY ~atweaks/eff/fey/rr#fcsng.eff~ ~override~                    // Charming Song EFF 1 (for targeting only humanoids)
COPY ~atweaks/eff/fey/rr#fcsn2.eff~ ~override~                    // Charming Song EFF 2 (for elven and half-elven charm immunity)
  SAY 0x1C @1903                                                  // Resists charm effect

// Items and spells which cause Deafness should grant immunity to the Sirine's Charming Song
ACTION_FOR_EACH ~file~ IN
    ~spwi223~  // Deafness
    ~rr#bhl5a~ // Resonating Weapon (RR)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.spl~ BEGIN
    COPY_EXISTING ~%file%.spl~ ~override~
      PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN  // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
        LPF add_conditional_immunity INT_VAR f_Condition = 80 f_Strref = 4742 STR_VAR f_Spell = rr#fcsng END
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END

COPY ~atweaks/spl/fey/rr#fdsnr.spl~ ~override~       // Detect Snares and Pits (renewable)
  SAY NAME1 @1904 SAY NAME2 @1904

COPY ~atweaks/vvc/rr#mmswf.vvc~ ~override~           // Fog Cloud animation
COPY ~atweaks/spl/fey/rr#fcld.spl~ ~override~        // Fog Cloud

COPY ~atweaks/spl/fey/rr#fdsnr.spl~ ~override~       // Detect Snares and Pits (renewable)
  SAY NAME1 @1904 SAY NAME2 @1904

COPY ~atweaks/spl/fey/rr#ftrnq.spl~ ~override~       // Touch of Tranquility (spell)
  SAY NAME1 @1905 SAY NAME2 @1905

COPY ~atweaks/bam/fey/rr#fspkp.bam~ ~override~       // Speak With Plants icon
COPY ~atweaks/spl/fey/rr#fspkp.spl~ ~override~       // Speak With Plants (renewable)
  SAY NAME1 @1907 SAY NAME2 @1907
  LPF add_conditional_immunity INT_VAR f_Condition = 169 f_Strref = 4742 STR_VAR f_Spell = SPPR105 END // Entangle (Priest)
  LPF add_conditional_immunity INT_VAR f_Condition = 169 f_Strref = 4742 STR_VAR f_Spell = SPWM111 END // Entangle (Wild Mage)
  LPF add_conditional_immunity INT_VAR f_Condition = 169 f_Strref = 4742 STR_VAR f_Spell = SPIN688 END // Plant Growth (Black Dragon)
  LPF add_conditional_immunity INT_VAR f_Condition = 169 f_Strref = 4742 STR_VAR f_Spell = RR#SMENT END // Shambler Entangle (RR)
  LPF add_conditional_immunity INT_VAR f_Condition = 169 f_Strref = 4742 STR_VAR f_Spell = RR#FENTG END // Hamadryad Entangle (aTweaks)
  PATCH_IF MOD_IS_INSTALLED ~SETUP-SPELLPACKB6.TP2~ ~2731~ BEGIN
    SET DEST_RES = (IDS_OF_SYMBOL (~spell~ ~WIZARD_CHARM_PLANTS~)-2000)
	  SPRINT spell_id "sppr%DEST_RES%"
	  LPF add_conditional_immunity INT_VAR f_Condition = 169 f_Strref = 4742 STR_VAR f_Spell = EVAL ~%spell_id%~ END // charm Plants (SpellPack)
  END

ADD_PROJECTILE ~atweaks/pro/rr#fentg.pro~            // New projectile for Entangle (party friendly)
COPY ~atweaks/eff/fey/rr#fentg.eff~ ~override~       // Entangle EFF (for incorporeal and large creature immunity)
COPY ~atweaks/spl/fey/rr#fentg.spl~ ~override~       // Entangle
  READ_LONG  0x64 ab_off
  READ_SHORT 0x68 ab_num
  FOR(i=0; i<ab_num; i+=1) BEGIN
    WRITE_SHORT (ab_off+i*0x28+0x26) %rr#fentg%      // use new projectile
  END

COPY ~atweaks/spl/fey/rr#fiinv.spl~ ~override~       // Improved Invisibility shell spell (use Casting Level 11th)
COPY_EXISTING ~SPWI405.SPL~  ~override/rr#wi405.spl~ // Clone Improved Invisibility into an innate ability (used by Sirine)
  LPF spellToInnate INT_VAR ctime=1 END

COPY ~atweaks/spl/fey/rr#pjell.spl~ ~override~       // Polymorph Mustard Jelly (Sirine)
  ADD_PROJECTILE ~atweaks/pro/rr#ftvap.pro~          // New projectile for Toxic Vapors (small area cloud)

COPY ~atweaks/spl/fey/rr#ftvap.spl~ ~override~       // Mustard Jelly Toxic Vapors
  SAY NAME1 @1906 SAY NAME2 @1906
  READ_LONG  0x64 ab_off
  READ_SHORT 0x68 ab_num
  FOR(i=0; i<ab_num; i+=1) BEGIN
    WRITE_SHORT (ab_off+i*0x28+0x26) %rr#ftvap%      // use new projectile
  END
  FOR (i = LONG_AT 0x6a; i < SOURCE_SIZE; i += 0x30) BEGIN
    READ_BYTE  i + 0xc timing
    WRITE_LONG i + 0xe timing = 1 ? 0 : 12 // 2 rounds for non-instant effects
  END

COPY ~atweaks/cre/fey/rr#pjell.cre~ ~override~  // Mustard Jelly (PnP style)
COPY ~atweaks/itm/fey/rr#pjell.itm~ ~override~  // Mustard Jelly attack (PnP style)
COPY ~atweaks/itm/fey/rr#fsiri.itm~ ~override~  // Sirine's Boots (movement rate increase, destroyed when polymorphed)
