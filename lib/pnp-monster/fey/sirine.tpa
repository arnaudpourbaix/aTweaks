ACTION_FOR_EACH ~file~ IN
  SIRINE    // Sirine (BGT)
  _SIRINE   // Sirine (Tutu)
  SIRINE_A  // Sirine (BGT)
  _IRINE_A  // Sirine (Tutu)
  SIRINE_B  // Sirine (BGT)
  _IRINE_B  // Sirine (Tutu)
  SIRINE01  // Sirine
  SIRINE02  // Sirine (BGT)
  _IRINE02  // Sirine (Tutu)
  SIL       // Sil (BGT)
  _SIL      // Sil (Tutu)
  J#SIRIN1 	// Sirine (Jarl Adv)
  J#SIRIN2 	// Sirine (Jarl Adv)
  L#NDC1		// Southern Edge
  NTSILUA 	// Sirine (NTotSC)
  NTSIRIN2 	// Sirine (NTotSC)
  NTSIRIN4 	// Sirine (NTotSC)
  NTSIRINE 	// Sirine (NTotSC)			  
  ISLSIR 		// Sirine Queen (BGT)
  MEIALA 		// Sirine (BGT)
  QSEROMOD 	// Sirine (PofQuestPack)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF FJ_CRE_VALIDITY END
      REMOVE_KNOWN_SPELLS
      REMOVE_MEMORIZED_SPELLS
      ADD_MEMORIZED_SPELL ~rr#fcsng~ #0 ~innate~ (1)                                     // adds 1x Charming Song
      ADD_MEMORIZED_SPELL ~rr#fcld~ #0 ~innate~ (1)                                      // adds 1x Fog Cloud
      ADD_MEMORIZED_SPELL ~rr#wi405~ #0 ~innate~ (1)                                     // adds 1x Improved Invisibility
      ADD_MEMORIZED_SPELL ~rr#pjell~ #0 ~innate~ (1)                                     // adds 1x Polymorph Mustard Jelly
      ADD_CRE_ITEM ~rr#fsiri~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~boots~                 // add boots which increase Movement Rate to 9 (Sirines only because of Polymorph)
      REMOVE_CRE_ITEM ~SIRINE~                                                           // removes SIRINE.ITM on BGT (Touch of Tranquility is handled differently)
      REMOVE_CRE_ITEM ~_SIRINE~                                                          // removes _SIRINE.ITM on Tutu (Touch of Tranquility is handled differently)
      REMOVE_CRE_ITEM ~SIRINE1~                                                          // removes SIRINE1.ITM on BGT (Touch of Tranquility is handled differently)
      REMOVE_CRE_ITEM ~_SIRINE1~                                                         // removes _SIRINE1.ITM on Tutu (Touch of Tranquility is handled differently)
      ADD_CRE_ITEM ~rr#ftrnq~ #0 #0 #0 ~UNDROPPABLE~ ~weapon2~                           // add Touch of Tranquility
      ADD_CRE_ITEM ~AROW01~ #40 #0 #0 ~UNSTEALABLE~ ~quiver3~                            // add 40 normal arrows
      ADD_CRE_ITEM ~AROW10~ #10 #0 #0 ~UNSTEALABLE~ ~quiver2~                            // add 10 Arrows of Piercing
      REPLACE_CRE_ITEM ~AROW05~ #5 #0 #0 ~UNSTEALABLE~ ~quiver1~  EQUIP                  // make the Arrows of Biting unstealable and equip them
      LPF ADD_CRE_ITEM_FLAGS STR_VAR item_to_change = "BOW05" flags = UNSTEALABLE END    // make the Shortbow unstealable (BGT)
      LPF ADD_CRE_ITEM_FLAGS STR_VAR item_to_change = "_BOW05" flags = UNSTEALABLE END   // make the Shortbow unstealable (Tutu)
      WRITE_BYTE 0x6f 1                                                                  // BG1 Proficiency slot (small sword)
      WRITE_BYTE 0x70 1                                                                  // BG1 Proficiency slot (bow)
      DEFINE_ARRAY notEnforceFiles BEGIN ISLSIR MEIALA QSEROMOD END
      LPF patchCreature
        INT_VAR
          level1=5
          strength=14
          dexterity=18
          constitution=11
          intelligence=18
          wisdom=16
          charisma=17
          movement=9
          hp=40
          ac=3
          thac0=15
          apr=1
          xpv=3000
          alignment=50        // Chaotic Neutral
          saveDeath=11
          saveWand=7
          savePolymorph=9
          saveBreath=11
          saveSpell=8
          resistMagic=20
          morale=12
          moraleBreak=4
          moraleRecovery=15
          general=1           // HUMANOID
          race=120            // FAIRY
          class=130           // FAIRY_SIRINE
          gender=2
          sex=2
          enforce=1
        STR_VAR
          notEnforceFiles
      END
      DEFINE_ARRAY removeScripts BEGIN 
        SHOUT       // generic shout script
        MACSHOUT    // generic shout script (Tutu)
        SIRINE      // default Sirine script
        SIRSPELL    // default Sirine script
        _IRSPELL    // Sirine script (Tutu)
        WTARSGT     // generic AI script
        _WTARSGT    // generic AI script (Tutu)
        SIL         // Sil's script
        INITDLG     // dialogue script (not needed as it's handled within the new script)
        _INITDLG    // dialogue script (Tutu)
        wdarsgt     // generic AI script
        _WDARSGT    // generic AI script (Tutu)
        BPWTRSGT    // Big Picture generic AI script
        CRESHOUT    // Big Picture shout script
        DW2RC2MO    // Stratagems ranged AI script
        DW2RC2MW    // Stratagems ranged AI script
        DW2RC2GE    // Stratagems ranged AI script
        DW2MM2MO    // Stratagems ranged AI script
      END
      LPF patchCreatureScript STR_VAR script=RR#FSIRI removeScripts END
      LPF cloud_spell_immunities END
      PATCH_IF (("%SOURCE_RES%" STRING_EQUAL_CASE ~SIL~) OR ("%SOURCE_RES%" STRING_EQUAL_CASE ~_SIL~)) BEGIN 
        // special case: Sil should be a 7 HD Sirine
        WRITE_LONG 0x14 5000 // XP value
        WRITE_SHORT 0x24 56  // current hit points
        WRITE_SHORT 0x26 56  // maximum hit points
        WRITE_BYTE 0x52 13   // THAC0
        WRITE_BYTE 0x54 10   // save vs. death
        WRITE_BYTE 0x55 12   // save vs. wands
        WRITE_BYTE 0x56 11   // save vs. polymorph
        WRITE_BYTE 0x57 12   // save vs. breath
        WRITE_BYTE 0x58 13   // save vs. spells
        WRITE_BYTE 0x234 7   // level (first class)
        REMOVE_CRE_ITEM ~BOW05~ // special case, removes Shortbow (BGT)
        REMOVE_CRE_ITEM ~_BOW05~ // special case, removes Shortbow (Tutu)
        READ_LONG  0x2b8 "slot_off" ELSE 0
        READ_LONG  0x2bc "itm_off" ELSE 0
        READ_LONG  0x2c0 "itm_num" ELSE 0
        READ_SHORT (%slot_off% + (9 * 0x02)) weap1 // WEAPON1 slot
        PATCH_IF %weap1% = 0xffff BEGIN // if the WEAPON1 slot is empty
          ADD_CRE_ITEM ~BOW05~ #0 #0 #0 ~UNSTEALABLE~ ~weapon1~ EQUIP TWOHANDED // add Shortbow
        END
        REPLACE_CRE_ITEM ~AROW10~ #15 #0 #0 ~UNSTEALABLE~ ~quiver2~ // give Sil 15 Arrows of Piercing
        REPLACE_CRE_ITEM ~AROW05~ #10 #0 #0 ~UNSTEALABLE~ ~quiver1~ EQUIP // give Sil 10 Arrows of Biting
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END
