ACTION_FOR_EACH ~file~ IN
  VAELASA   // Vaelasa (Fairy Queen in Windsper Hills)
  DRYADHA   // Hamadryad (BGT)
  _DRYADHA  // Hamadryad (Tutu)
  //HAMASU    // Spell Revisions summoned Hamadryad (do not touch unless reviewing spell Call Woodland Beings)
  WQXHAMA   // White Queen
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF FJ_CRE_VALIDITY END
      REMOVE_KNOWN_SPELLS
      REMOVE_MEMORIZED_SPELLS
      ADD_MEMORIZED_SPELL ~rr#fchrm~ #0 ~innate~ (3)  // adds 3x Charm Person
      ADD_MEMORIZED_SPELL ~rr#fentg~ #0 ~innate~ (1)  // adds 1x Entangle
      ADD_MEMORIZED_SPELL ~rr#fafrn~ #0 ~innate~ (1)  // adds 1x Animal Friendship
      ADD_MEMORIZED_SPELL ~rr#fdsnr~ #0 ~innate~ (1)  // adds 1x Detect Snares and Pits
      ADD_MEMORIZED_SPELL ~rr#fspkp~ #0 ~innate~ (1)  // adds 1x Speak With Plants
      ADD_MEMORIZED_SPELL ~rr#ddoor~ #0 ~innate~ (1)  // adds 1x Dimension Door
      /* TODO: create spell Quench Fire:
       * You extinguish all fires in a 30-foot cube centered on a point you choose within range. Any nonmagical fire is put out automatically, as are magical flames created by a spell of 3rd level or lower. For each
       * spell of 4th level or higher which is creating flame within this area, make an ability check using your spellcasting ability. On a successful check, the spell that created the fire ends. Fire created by a magical
       * item is also doused, and the item becomes unable to produce fire for 1d4 hours.
       * Save vs spell to negate effect.
       * Affected spells: Agannazar’s Scorcher, Melf’s Minute Meteors, Flame Blade
       * Radius and projectile as level 7 Priest Fire Storm
       */
      DEFINE_ARRAY notEnforceFiles BEGIN wqxhama END
      LPF patchCreature 
        INT_VAR
          level1=4
          strength=10
          dexterity=18
          constitution=12
          intelligence=14
          wisdom=14
          charisma=18
          movement=11
          hp=32
          ac=7
          thac0=17
          apr=1
          xpv=1400           // 650 in P&P, but it makes no sense as they clearly superior in every ways to dryads (975xp)
          missileAC=0        // remove missile AC modifier (was -5)
          alignment=34       // True Neutral
          saveDeath=9
          saveWand=13
          savePolymorph=12
          saveBreath=15
          saveSpell=14
          resistMagic=75
          morale=12
          moraleBreak=4
          moraleRecovery=15
          general=1          // HUMANOID
          race=120           // FAIRY
          class=131          // FAIRY_DRYAD
          gender=2
          sex=2
          enforce=1
        STR_VAR
          proficiencies=~96 => 1~ // dagger
          notEnforceFiles
      END
      DEFINE_ARRAY removeScripts BEGIN 
        WTASIGHT  // generic AI script
        _TASIGHT  // generic AI script (Tutu)
        HAMA      // default Hamadryad AI script
        _HAMA     // default Hamadryad AI (Tutu)
        SHOUTDLG  // dialogue script (not needed as it's handled within the new script)
        vaelasa   // Vaelasa's script (not needed as it's handled within the new script)
        BPWTSIGT  // Big Picture generic AI script
        BPWDASGT  // Big Picture generic AI script
        DW2MP2GE  // Stratagems Melee script
        DW2MP0GE  // Stratagems Melee script
      END
      DEFINE_ARRAY skipFiles BEGIN 
        wqxhama	  // White queen special script
      END
      LPF patchCreatureScript STR_VAR script=RR#FHAMA removeScripts skipFiles END
      LPF ~entangle_immunities~ END
    BUT_ONLY_IF_IT_CHANGES
  END
END
// ACTION_FOR_EACH file IN
//   HAMASU    // Spell Revisions summoned Hamadryad
// BEGIN
//   ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
//     COPY_EXISTING ~%file%.cre~ ~override~
//       LPF make_summon END
//       WRITE_ASCII SCRIPT_OVERRIDE "RR#FHAMA" #8
//     BUT_ONLY
//   END
// END
