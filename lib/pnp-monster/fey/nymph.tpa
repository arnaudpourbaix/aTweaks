ACTION_FOR_EACH ~file~ IN
  ABELA      // Abela the Nymph (BGT)
  _ABELA     // Abela the Nymph (Tutu)
  HGNYMPH    // Nymph
  NYMPHSU    // default summoned Nymph
  NYMPHSUM   // summoned Nymph
  //dvnymph  // Spell Revisions summoned Nymph (do not touch unless reviewing spell Call Woodland Beings)
  dw#nymsu   // SCSII summoned Nymph
  WQXNYM		 // White Queen
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF FJ_CRE_VALIDITY END
      REMOVE_KNOWN_SPELLS
      REMOVE_MEMORIZED_SPELLS
      ADD_MEMORIZED_SPELL ~rr#ddor2~ #0 ~innate~ (1)                // adds 1x Dimension Door (non-renewable)
      ADD_MEMORIZED_SPELL ~SPPR101~ #0 ~priest~ (2)                 // adds 2x Bless
      ADD_MEMORIZED_SPELL ~SPPR103~ #0 ~priest~ (2)                 // adds 2x Cure Light Wounds
      ADD_MEMORIZED_SPELL ~SPPR202~ #1 ~priest~ (1)                 // adds 1x Barkskin
      ADD_MEMORIZED_SPELL ~SPPR204~ #1 ~priest~ (1)                 // adds 1x Charm Person or Mammal
      ADD_MEMORIZED_SPELL ~SPPR302~ #2 ~priest~ (1)                 // adds 1x Call Lightning
      ADD_MEMORIZED_SPELL ~SPPR319~ #2 ~priest~ (1)                 // adds 1x Summon Insects
      ADD_MEMORIZED_SPELL ~SPPR404~ #3 ~priest~ (1)                 // adds 1x Neutralize Poison
      REPLACE_CRE_ITEM ~rr#nymp~ #0 #0 #0 ~UNDROPPABLE~ ~WEAPON1~   // replace defaul weapon with a non-damaging attack
      REPLACE_CRE_ITEM ~rr#nymp~ #0 #0 #0 ~UNDROPPABLE~ ~WEAPON2~   // replace defaul weapon with a non-damaging attack
      DEFINE_ARRAY notEnforceFiles BEGIN WQXNYM dw#nymsu END
      LPF patchCreature
        INT_VAR
          level1=7            // 3 hd, but cast spells as 7th level druid
          strength=10
          dexterity=17
          constitution=12
          intelligence=16
          wisdom=12
          charisma=19
          movement=9
          hp=24
          ac=9
          thac0=17
          apr=0
          xpv=1400
          alignment=33        // Neutral Good
          saveDeath=9
          saveWand=13
          savePolymorph=12
          saveBreath=15
          saveSpell=14
          resistMagic=50
          morale=7
          moraleBreak=2
          moraleRecovery=15
          general=1           // HUMANOID
          race=120            // FAIRY
          class=11            // Druid
          gender=2
          sex=2
          enforce=1
        STR_VAR
          notEnforceFiles
      END
      DEFINE_ARRAY removeScripts BEGIN 
        WTASIGHT   // generic AI script
        wtrunsgt   // generic AI script
        _TRUNSGT   // generic AI script (Tutu)
        hgnymph    // original hostile Nymph script
        nymph      // original summoned Nymph script
        DVNYMPH    // Spell Revisions' summoned Nymph script
        DVMELEE    // Spell Revisions' melee script
        dw#nymph   // SCSII Nymph script
        BPNYMPH    // Big Picture Nymph script
        BPWDASGT   // Big Picture generic AI script
        BPWTSIGT   // Big Picture generic AI script
        DW2MS2GE   // Stratagems melee AI script
      END
      LPF patchCreatureScript STR_VAR script=RR#FNYMP removeScripts END
    BUT_ONLY_IF_IT_CHANGES
  END
END
ACTION_FOR_EACH file IN
  NYMPHSUM
  NYMPHSU
  //dvnymph   // Spell Revisions summoned Nymph (do not touch unless reviewing spell Call Woodland Beings)
  dw#nymsu
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_summon END
      WRITE_ASCII SCRIPT_OVERRIDE "RR#SNYMP" #8
    BUT_ONLY
  END
END
