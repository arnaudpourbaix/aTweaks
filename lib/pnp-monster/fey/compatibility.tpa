// Quest Pack compatibility block (for the "New Fate For The Dryads' Acorns" component)
ACTION_IF FILE_EXISTS_IN_GAME ~sufake.itm~ AND FILE_EXISTS ~questpack/phluafae/appendvae.baf~ BEGIN
  <<<<<<<<%MOD_FOLDER%/inlined/baf/fey/vaelasa.baf
  IF
    False()
    Global("rr#ph4qpack","LOCALS",0)
  THEN
    RESPONSE #100
      Continue()
  END
  >>>>>>>>

  COPY_EXISTING ~rr#fhama.bcs~ ~override~
    REPLACE_BCS_BLOCK ~%MOD_FOLDER%/inlined/baf/fey/vaelasa.baf~ ~questpack/phluafae/appendvae.baf~
END



// The Calling compatibility block : add some black pearls to the game
ACTION_IF FILE_EXISTS_IN_GAME ~cdbpearl.itm~  BEGIN
  <<<<<<<<%MOD_FOLDER%/inlined/baf/fey/black_peal.baf
  IF
    HasItem("%TUTU_VAR%misc36",Myself)
    Global("CDPearls","LOCALS",0)
  THEN
    RESPONSE #95
      SetGlobal("CDPearls","LOCALS",1)
      Continue()
    RESPONSE #5
      SetGlobal("CDPearls","LOCALS",1)
      TakeItemReplace("cdbpearl","%TUTU_VAR%misc36",Myself)
      Continue()
  END
  >>>>>>>>

  EXTEND_TOP  ~rr#fsiri.bcs~  ~%MOD_FOLDER%/inlined/baf/fey/black_peal.baf~ EVALUATE_BUFFER // give sirines a chance to have black pearls
END



// Replace shouts with Help() in specific dialogue files
ACTION_FOR_EACH ~file~ IN
  sil   // Sil's dialogue (BGT)
  _SIL  // Sil's dialogue (Tutu)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.dlg~ BEGIN
    COPY_EXISTING ~%file%.dlg~ ~override~
      PATCH_IF (%SOURCE_SIZE% > 0x01) THEN BEGIN
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY EXACT_MATCH ~Shout(1)~ ~Help()~
        END
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END


ACTION_IF GAME_INCLUDES ~soa~ BEGIN
  // Vaelasa should summoned Dryads instead of Sirines
  COPY_EXISTING ~dry01.cre~    ~override/rr#fdrya.cre~                               // Hostile Dryad
    WRITE_LONG   0x10 2                                                              // clear flags, leave only "no corpse"
    WRITE_ASCIIE 0x248 "%DEST_RES%" #8                                               // Override script
    WRITE_ASCII  0x250 ~~ #8                                                         // disable class AI script
    WRITE_ASCII  0x258 ~~ #8                                                         // disable race AI script
    WRITE_ASCII  0x260 ~~ #8                                                         // disable general AI script
    WRITE_ASCII  0x268 ~~ #8                                                         // disable default AI script
    WRITE_BYTE   0x270 255                                                           // set intial allegiance to Enemy
    WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32                                              // death variable
    WRITE_ASCII  0x2cc ~~ #8                                                         // dialogue file
    READ_LONG 0x2bc io
    FOR (i=0;i<LONG_AT 0x2c0;++i) BEGIN                                              // Make all items unstealable&undroppable
      WRITE_BYTE io + 0x14*i + 0x10 THIS BOR 10
    END

  COPY_EXISTING ~AR1200.BCS~ ~override~                                              // Windsper Hills area script
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH ~SIRINE01~ ~rr#fdrya~
    END
  BUT_ONLY_IF_IT_CHANGES

  // The Spellhold Asylum door should be unlocked and opened once (note: remove this fix once it's included in the BG2 Fixpack)
  COPY_EXISTING ~ar1500.bcs~ ~override~
    PATCH_IF NOT FILE_CONTAINS_EVALUATED (~%SOURCE_FILE%~ ~RR#Door1501~) BEGIN         // prevent duplication in case the Fixpack version is installed
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY EXACT_MATCH ~GlobalGT("AsylumPlot","GLOBAL",55)~
                                      ~GlobalGT("AsylumPlot","GLOBAL",55) Global("RR#Door1501","AR1500",0)~
        REPLACE_TEXTUALLY EXACT_MATCH ~Unlock("Door1501")~
                                      ~SetGlobal("RR#Door1501","AR1500",1) Unlock("Door1501")~
      END
    END
  BUT_ONLY_IF_IT_CHANGES

  // Spawn Sirines instead of Lizardmen and Mists in Spellhold
  <<<<<<<<%MOD_FOLDER%/inlined/baf/fey/rr#1500.baf
  IF
	Global("rr#spwn","AR1500",0)
	OR(2)
	  XPGT(Player1,1999999)
          DifficultyGT(EASY) // Core Rules difficulty or higher
  THEN
	RESPONSE #100
		SetGlobal("rr#spwn","AR1500",1)
		SpawnPtDeactivate("Spawn Point 2") // Lizardmen
		SpawnPtDeactivate("Spawn Point 3") // Mists
		CreateCreature("SIRINE01",[1550.2530],0) // Sirine (center)
		CreateCreature("SIRINE01",[1500.2550],4) // Sirine (bottom left)
		CreateCreature("SIRINE01",[1600.2550],15) // Sirine (bottom right)
		CreateCreature("SIRINE01",[1500.2485],9) // Sirine (top left)
		CreateCreature("SIRINE01",[1600.2485],12) // Sirine (top right)
  END

  IF
	Global("rr#spwn","AR1500",0)
	OR(2)
          XPGT(Player1,1499999)
          DifficultyGT(EASIEST) // Normal difficulty or higher
  THEN
	RESPONSE #100
		SetGlobal("rr#spwn","AR1500",1)
		SpawnPtDeactivate("Spawn Point 2") // Lizardmen
		SpawnPtDeactivate("Spawn Point 3") // Mists
		CreateCreature("SIRINE01",[1550.2530],0) // Sirine (center)
		CreateCreature("SIRINE01",[1500.2550],4) // Sirine (bottom left)
		CreateCreature("SIRINE01",[1500.2485],9) // Sirine (top left)
  END

  IF
	Global("rr#spwn","AR1500",0)
	OR(2)
          XPLT(Player1,1500000)
          Difficulty(EASIEST) // Easiest difficulty level
  THEN
	RESPONSE #100
		SetGlobal("rr#spwn","AR1500",1)
		SpawnPtDeactivate("Spawn Point 2") // Lizardmen
		SpawnPtDeactivate("Spawn Point 3") // Mists
		CreateCreature("SIRINE01",[1550.2530],0) // Sirine (center)
  END
  >>>>>>>>

  EXTEND_BOTTOM ~AR1500.BCS~  ~%MOD_FOLDER%/inlined/baf/fey/rr#1500.baf~               // extend the Spellhold area script
END

// Give the Cloakwood Hamadryad a few allies
<<<<<<<<%MOD_FOLDER%/inlined/baf/fey/rr#chama.baf
IF
	Global("rr#chama","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("rr#chama","MYAREA",1)
                CreateCreature("BEARCA01",[1070.3330],0) // Cave Bear
                CreateCreature("BEARCA01",[1200.3330],0) // Cave Bear
                CreateCreature("WOLFDI01",[1065.3390],0) // Dire Wolf
                CreateCreature("WOLFDI01",[1110.3420],0) // Dire Wolf
                CreateCreature("WOLFDI01",[1165.3420],0) // Dire Wolf
                CreateCreature("WOLFDI01",[1190.3390],0) // Dire Wolf
		Continue()
END
>>>>>>>>

ACTION_IF FILE_EXISTS_IN_GAME ~AR8500.BCS~ BEGIN
  EXTEND_BOTTOM ~AR8500.BCS~  ~%MOD_FOLDER%/inlined/baf/fey/rr#chama.baf~               // extend the Cloakwood 4 area script (BGT)
END

ACTION_IF FILE_EXISTS_IN_GAME ~_AR1700.BCS~ BEGIN
  EXTEND_BOTTOM ~_AR1700.BCS~  ~%MOD_FOLDER%/inlined/baf/fey/rr#chama.baf~              // extend the Cloakwood 4 area script (Tutu)
END

ACTION_IF FILE_EXISTS_IN_GAME ~BG1700.BCS~ BEGIN
  EXTEND_BOTTOM ~BG1700.BCS~  ~%MOD_FOLDER%/inlined/baf/fey/rr#chama.baf~              // extend the Cloakwood 4 area script (EET)
END

ACTION_IF GAME_IS ~iwdee~ BEGIN                                                   // use the proper dimension door animation on IWD:EE
  COPY_EXISTING ~rr#ddoor.spl~ ~override~                                         // Dimension Door
                ~rr#ddor2.spl~ ~override~                                         // Dimension Door
    WRITE_ASCII 0x10 ~#EFF_M09~ #8 // casting sound
    LPF ALTER_EFFECT INT_VAR check_globals = 0 check_headers = 1 match_opcode = 215 STR_VAR resource = ~DDOORH~ END
    LPF ALTER_EFFECT INT_VAR check_globals = 0 check_headers = 1 match_opcode = 174 STR_VAR resource = ~#EFF_M09~ END
  BUT_ONLY
END



// Item Revisions
ACTION_IF MOD_IS_INSTALLED ITEM_REV.TP2 0 BEGIN                               		// Item Revisions mod check
  ACTION_IF NOT FILE_EXISTS_IN_GAME ~rr#fake.itm~ BEGIN
    COPY_EXISTING ~sw1h01.itm~ ~override/rr#fake.itm~
      WRITE_LONG 0x1e "-1"
      SAY NAME2 ~Fake item~
  END

  // Compatibility with IR: script's check for CLCK26 and CLCK24 are not sensible, replace them with a fake item
  ACTION_FOR_EACH script IN  rr#fnymp rr#snymp BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%script%.bcs~ BEGIN
      COPY_EXISTING ~%script%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
          ~clck26~
          ~rr#fake~ // I am a bit lazy but this will do the trick and is easier in this case than R_B_B
          REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
          ~clck24~
          ~rr#fake~ // idem
        END
      BUT_ONLY
    END
  END
END