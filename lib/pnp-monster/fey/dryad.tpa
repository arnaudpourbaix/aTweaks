ACTION_FOR_EACH ~file~ IN
  DRYAD    // Dryad of the Cloudpeaks (BGT)
  _DRYAD   // Dryad of the Cloudpeaks (Tutu)
  DRY01    // Dryad (unused?)
  DRYAD01  // Dryad (unused?)
  DRYAD02  // Dryad (unused?)
  DRYAD03  // Dryad (unused?)
  IDRYAD01 // Ulene (one of Irenicus captive Dryads)
  IDRYAD02 // Cania (one of Irenicus captive Dryads)
  IDRYAD03 // Elyme (one of Irenicus captive Dryads)
  dr01mod  // Dryad (PofQuestPack)
  drmod    // Dryad (PofQuestPack)
  RE_DRYAD // Dryad (PofQuestPack)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF FJ_CRE_VALIDITY END
      REMOVE_KNOWN_SPELLS
      REMOVE_MEMORIZED_SPELLS
      ADD_MEMORIZED_SPELL ~rr#fchrm~ #0 ~innate~ (3)  // adds 3x Charm Person
      ADD_MEMORIZED_SPELL ~rr#fspkp~ #0 ~innate~ (1)  // adds 1x Speak With Plants
      ADD_MEMORIZED_SPELL ~rr#ddoor~ #0 ~innate~ (1)  // adds 1x Dimension Door
      DEFINE_ASSOCIATIVE_ARRAY proficiencies BEGIN 96 => 1 END // dagger      
      DEFINE_ARRAY notEnforceFiles BEGIN dr01mod RE_DRYAD END
      LPF patchCreature   
        INT_VAR 
          level1=2
          strength=10
          dexterity=15
          constitution=12
          intelligence=14
          wisdom=14
          charisma=18
          movement=9
          hp=16
          ac=9
          thac0=19
          apr=1
          xpv=975
          alignment=34       // True Neutral
          saveDeath=14
          saveWand=16
          savePolymorph=15
          saveBreath=17
          saveSpell=17
          resistMagic=50
          morale=12
          moraleBreak=4
          moraleRecovery=15
          general=1          // HUMANOID
          race=120           // FAIRY
          class=131          // FAIRY_DRYAD
          gender=2
          sex=2
          enforce=1
        STR_VAR 
          proficiencies
          notEnforceFiles
      END
      DEFINE_ARRAY removeScripts BEGIN 
        CDRYAD        // Dryad AI script
        _CDRYAD       // Dryad AI script (Tutu)
        DRYAD         // Dryad AI script
        INITDLG       // dialogue script (not needed as it's handled within the new script)
        _INITDLG      // dialogue script (Tutu)
        SHOUT         // generic shout script
        WTASIGHT      // generic AI script
        wtrunsgt      // generic AI script
        _TRUNSGT      // generic AI script (Tutu)
        IDRYAD01      // Ulene's dialogue script (not needed as it's handled within the new script)
        ULENE         // Ulene's AI script (not needed as it's handled within the new script)
        BPWDASGT      // Big Picture generic AI script
        SIRSPELL      // default Sirine script
        DW2MP0GE      // Stratagems Melee script
        DW2RC2MO      // Stratagems Melee script
      END
      DEFINE_ARRAY skipFiles BEGIN 
        drmod         // PofQuestPack special script
      END
      LPF patchCreatureScript STR_VAR script=RR#FDRYA removeScripts skipFiles END
      PATCH_IF "%SOURCE_RES%" STRING_EQUAL_CASE ~DRYAD~ OR "%SOURCE_RES%" STRING_EQUAL_CASE ~_DRYAD~ BEGIN 
          // special case, Dryad of the Cloudpeaks: class = INNOCENT
          WRITE_BYTE 0x273 155
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END
