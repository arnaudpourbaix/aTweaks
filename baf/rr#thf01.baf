// Title: Simple Thief script
// Date Created: September 24, 2002
// Created by: aVENGER
// Note: doesn't require Detectable Spells


// Switch between constantly searching for traps and hiding (the "D" key toggles between modes)

IF
	Global("RR#THFS","LOCALS",0)
	HotKey(D)
THEN
	RESPONSE #100
		SetGlobal("RR#THFS","LOCALS",1)
		DisplayStringHead(Myself,@223) // Attempting to hide in shadows
END

IF
	Global("RR#THFS","LOCALS",1)
	HotKey(D)
THEN
	RESPONSE #100
		SetGlobal("RR#THFS","LOCALS",0)
		DisplayStringHead(Myself,@224) // Searching for traps
END


// Switch between attacking enemies and not attacking (the "V" key toggles between modes)

IF
	Global("RR#THFA","LOCALS",0)
	HotKey(V)
THEN
	RESPONSE #100
		SetGlobal("RR#THFA","LOCALS",1)
		DisplayStringHead(Myself,@235) // Attacking enemies
END

IF
	Global("RR#THFA","LOCALS",1)
	HotKey(V)
THEN
	RESPONSE #100
		SetGlobal("RR#THFA","LOCALS",0)
		DisplayStringHead(Myself,@236) // Not attacking enemies
END


// Search for traps whenever the character is idle (doesn't interrupt player input)

IF
	Global("RR#THFS","LOCALS",0)
	ActionListEmpty()
	OR(2)
	  !See([EVILCUTOFF])
	  Global("RR#THFA","LOCALS",0)
	OR(2)
	  Class(Myself,THIEF_ALL)
	  Class(Myself,MONK)
	!ModalState(TURNUNDEAD)
	!ModalState(DETECTTRAPS)
THEN
	RESPONSE #100
		FindTraps()
END


// Attempt to hide in shadows once per round whenever the character is idle (doesn't interrupt player input)

IF
	Global("RR#THFS","LOCALS",1)
	!GlobalTimerNotExpired("RR#HIDE","LOCALS")
	ActionListEmpty()
	!See([EVILCUTOFF])
	OR(3)
	  Class(Myself,THIEF_ALL)
	  Class(Myself,RANGER_ALL)
	  Class(Myself,MONK)
	!ModalState(TURNUNDEAD)
	!ModalState(STEALTH)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#HIDE","LOCALS",6)
		Hide()
END


// Attack the nearest enemy but not while under the effects of Invisibility and/or Sanctuary

IF
	Global("RR#THFA","LOCALS",1)
	ActionListEmpty()
	!StateCheck(Myself,STATE_INVISIBLE)
	CheckStatLT(Myself,1,SANCTUARY)
	See([EVILCUTOFF])
	!InParty([EVILCUTOFF])
THEN
	RESPONSE #100
		AttackReevaluate([EVILCUTOFF],30)
END