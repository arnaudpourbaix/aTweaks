// Title: Simple Thief script
// Date Created: September 24, 2002
// Created by: aVENGER
// Note: doesn't require Detectable Spells
// IWD-adapted by Wisp


// Switch between constantly searching for traps and hiding (the "D" key toggles between modes)

IF
	Global("RR#THFS","LOCALS",0)
	HotKey(D)
THEN
	RESPONSE #100
		SetGlobal("RR#THFS","LOCALS",1)
		FloatMessage(Myself,@223) // Attempting to hide in shadows
END

IF
	Global("RR#THFS","LOCALS",1)
	HotKey(D)
THEN
	RESPONSE #100
		SetGlobal("RR#THFS","LOCALS",0)
		FloatMessage(Myself,@224) // Searching for traps
END


// Switch between attacking enemies and not attacking (the "V" key toggles between modes)

IF
	Global("RR#THFA","LOCALS",0)
	HotKey(V)
THEN
	RESPONSE #100
		SetGlobal("RR#THFA","LOCALS",1)
		FloatMessage(Myself,@235) // Attacking enemies
END

IF
	Global("RR#THFA","LOCALS",1)
	HotKey(V)
THEN
	RESPONSE #100
		SetGlobal("RR#THFA","LOCALS",0)
		FloatMessage(Myself,@236) // Not attacking enemies
END


// Search for traps whenever the character is idle (doesn't interrupt player input)

IF
	Global("RR#THFS","LOCALS",0)
	ActionListEmpty()
	OR(2)
	  !See([EVILCUTOFF]%SeeDead%)
	  Global("RR#THFA","LOCALS",0)
        ClassEx(Myself,THIEF)
	!ModalState(Myself,TURNUNDEAD)
	!ModalState(Myself,DETECTTRAPS)
THEN
	RESPONSE #100
		FindTraps()
END


// Attempt to hide in shadows once per round whenever the character is idle (doesn't interrupt player input)

IF
	Global("RR#THFS","LOCALS",1)
	!GlobalTimerNotExpired("RR#HIDE","LOCALS")
	ActionListEmpty()
	!See([EVILCUTOFF]%SeeDead%)
	OR(2)
	  ClassEx(Myself,THIEF)
	  ClassEx(Myself,RANGER)
	!ModalState(Myself,TURNUNDEAD)
	!ModalState(Myself,STEALTH)
THEN
	RESPONSE #100
                SetGlobalTimer("RR#HIDE","LOCALS",5)
                Hide()
END


// Attack the nearest enemy but not while under the effects of Invisibility and/or Sanctuary

IF
	Global("RR#THFA","LOCALS",1)
	ActionListEmpty()
	!StateCheck(Myself,STATE_INVISIBLE)
	CheckStatLT(Myself,1,SANCTUARY)
	See([EVILCUTOFF]%SeeDead%)
	!InParty([EVILCUTOFF])
THEN
	RESPONSE #100
		AttackReevaluate([EVILCUTOFF],30)
END